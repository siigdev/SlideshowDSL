grammar org.xtext.example.mydsl.Anislide with org.eclipse.xtext.common.Terminals
generate anislide "http://www.xtext.org/example/mydsl/Anislide"

//  #################################
//  ## GLOBAL MODEL
//  #################################

Model:
	progressanimation+=ProgressAnimation* &
	template+=Template* &
	slides+=Slide* &
	global+=Global?
;

//  #################################
//  ## GLOBAL
//  #################################
Global:
	"Global" globalbody=GlobalBody	
;

GlobalBody:
	OPEN
		{GlobalBody} styles+=Style*
	CLOSE
;

//  #################################
//  ## PROGRESS ANIMATION
//  #################################

ProgressAnimation:
	"Progress" name=ID progressbody=ProgressBody
;

ProgressBody:
	OPEN
		{ProgressBody} progressentities+=ProgressEntity*
	CLOSE
;

ProgressEntity: 
		{progresstype} key="type:" value=AniType |
		{animate} key="animate:" value+=Options* ("->" tovalue=INT Unit)? 
;

//  #################################
//  ## SLIDE
//  #################################

Slide:
	"Slide" slidebody=SlideBody
;
SlideBody:
	OPEN
		{SlideBody} slideentities+=SlideEntity* styles+=Style*
	CLOSE
;

SlideEntity:
	{title} key='title:' value=STRING |
	{tmplt} key='template:' value=[Template]
;

//  #################################
//  ## TEMPLATE
//  #################################

Template:
	"Template" name=ID templatebody=TemplateBody
;

TemplateBody: 
	OPEN 
		{TemplateBody} templateentities+=TemplateEntity* styles+=Style*
	CLOSE
;

TemplateEntity: (
		{progressanim} key="progress:" value=[ProgressAnimation] |
		{tabular} key="line:" OPEN 'left:' left=BoxType 'right:' right=BoxType CLOSE
	)
;

//  #################################
//  ## GLOBAL
//  #################################

Options: 
	{height} key='height:' value=INT unit=Unit |
	{width} key='width:' value=INT unit=Unit |
	{position} key='position:' value=INT unit=Unit
;
Unit: 
	('px' | '%')
;

Style:
	{textcolor} key="color:" value=STRING |
	{backgroundcolor} key="background-color:" value=STRING |
	{fontsize} key="font-size:" value=INT 
;

AniType:
	"image" | "rectangle"
;
BoxType:
	'imagebox' | 'textbox'
;

//  #################################
//  ## TERMINALS
//  #################################

terminal OPEN:
	'('
;

terminal CLOSE:
	')'
;
terminal HEX:
	('0x'|'0X')('0'..'9'|'a'..'f'|'A'..'F')+
; 